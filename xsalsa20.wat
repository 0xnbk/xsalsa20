(module
  (memory (export "memory") 10 10)

  (func (export "core_hsalsa20") (param $out_ptr i32) (param $in_ptr i32) (param $key_ptr i32)
    ;; uint32_t x0, x1, x2, x3, x4, x5, x6, x7, x8,
    ;;          x9, x10, x11, x12, x13, x14,  x15;
    ;; int      i;

    (local $x0 i32)
    (local $x1 i32)
    (local $x2 i32)
    (local $x3 i32)
    (local $x4 i32)
    (local $x5 i32)
    (local $x6 i32)
    (local $x7 i32)
    (local $x8 i32)
    (local $x9 i32)
    (local $x10 i32)
    (local $x11 i32)
    (local $x12 i32)
    (local $x13 i32)
    (local $x14 i32)
    (local $x15 i32)
    (local $i i32)

    (set_local $x0 (i32.const 0x61707865))
    (set_local $x5 (i32.const 0x3320646e))
    (set_local $x10 (i32.const 0x79622d32))
    (set_local $x15 (i32.const 0x6b206574))

    (set_local $x1 (i32.load (get_local $key_ptr)))
    (set_local $x2 (i32.load (i32.add (get_local $key_ptr) (i32.const 4))))
    (set_local $x3 (i32.load (i32.add (get_local $key_ptr) (i32.const 8))))
    (set_local $x4 (i32.load (i32.add (get_local $key_ptr) (i32.const 12))))
    (set_local $x11 (i32.load (i32.add (get_local $key_ptr) (i32.const 16))))
    (set_local $x12 (i32.load (i32.add (get_local $key_ptr) (i32.const 20))))
    (set_local $x13 (i32.load (i32.add (get_local $key_ptr) (i32.const 24))))
    (set_local $x14 (i32.load (i32.add (get_local $key_ptr) (i32.const 28))))
    (set_local $x6 (i32.load (get_local $in_ptr)))
    (set_local $x7 (i32.load (i32.add (get_local $in_ptr) (i32.const 4))))
    (set_local $x8 (i32.load (i32.add (get_local $in_ptr) (i32.const 8))))
    (set_local $x9 (i32.load (i32.add (get_local $in_ptr) (i32.const 12))))

    ;; rounds
    (set_local $i (i32.const 20))
    (block $end_loop
      (loop $start_loop
        (br_if $end_loop (i32.eq (get_local $i) (i32.const 0)))
        (set_local $x4 (i32.xor (get_local $x4) (i32.rotl (i32.add (get_local $x0) (get_local $x12)) (i32.const 7))))
        (set_local $x8 (i32.xor (get_local $x8) (i32.rotl (i32.add (get_local $x4) (get_local $x0)) (i32.const 9))))
        (set_local $x12 (i32.xor (get_local $x12) (i32.rotl (i32.add (get_local $x8) (get_local $x4)) (i32.const 13))))
        (set_local $x0 (i32.xor (get_local $x0) (i32.rotl (i32.add (get_local $x12) (get_local $x8)) (i32.const 18))))
        (set_local $x9 (i32.xor (get_local $x9) (i32.rotl (i32.add (get_local $x5) (get_local $x1)) (i32.const 7))))
        (set_local $x13 (i32.xor (get_local $x13) (i32.rotl (i32.add (get_local $x9) (get_local $x5)) (i32.const 9))))
        (set_local $x1 (i32.xor (get_local $x1) (i32.rotl (i32.add (get_local $x13) (get_local $x9)) (i32.const 13))))
        (set_local $x5 (i32.xor (get_local $x5) (i32.rotl (i32.add (get_local $x1) (get_local $x13)) (i32.const 18))))
        (set_local $x14 (i32.xor (get_local $x14) (i32.rotl (i32.add (get_local $x10) (get_local $x6)) (i32.const 7))))
        (set_local $x2 (i32.xor (get_local $x2) (i32.rotl (i32.add (get_local $x14) (get_local $x10)) (i32.const 9))))
        (set_local $x6 (i32.xor (get_local $x6) (i32.rotl (i32.add (get_local $x2) (get_local $x14)) (i32.const 13))))
        (set_local $x10 (i32.xor (get_local $x10) (i32.rotl (i32.add (get_local $x6) (get_local $x2)) (i32.const 18))))
        (set_local $x3 (i32.xor (get_local $x3) (i32.rotl (i32.add (get_local $x15) (get_local $x11)) (i32.const 7))))
        (set_local $x7 (i32.xor (get_local $x7) (i32.rotl (i32.add (get_local $x3) (get_local $x15)) (i32.const 9))))
        (set_local $x11 (i32.xor (get_local $x11) (i32.rotl (i32.add (get_local $x7) (get_local $x3)) (i32.const 13))))
        (set_local $x15 (i32.xor (get_local $x15) (i32.rotl (i32.add (get_local $x11) (get_local $x7)) (i32.const 18))))
        (set_local $x1 (i32.xor (get_local $x1) (i32.rotl (i32.add (get_local $x0) (get_local $x3)) (i32.const 7))))
        (set_local $x2 (i32.xor (get_local $x2) (i32.rotl (i32.add (get_local $x1) (get_local $x0)) (i32.const 9))))
        (set_local $x3 (i32.xor (get_local $x3) (i32.rotl (i32.add (get_local $x2) (get_local $x1)) (i32.const 13))))
        (set_local $x0 (i32.xor (get_local $x0) (i32.rotl (i32.add (get_local $x3) (get_local $x2)) (i32.const 18))))
        (set_local $x6 (i32.xor (get_local $x6) (i32.rotl (i32.add (get_local $x5) (get_local $x4)) (i32.const 7))))
        (set_local $x7 (i32.xor (get_local $x7) (i32.rotl (i32.add (get_local $x6) (get_local $x5)) (i32.const 9))))
        (set_local $x4 (i32.xor (get_local $x4) (i32.rotl (i32.add (get_local $x7) (get_local $x6)) (i32.const 13))))
        (set_local $x5 (i32.xor (get_local $x5) (i32.rotl (i32.add (get_local $x4) (get_local $x7)) (i32.const 18))))
        (set_local $x11 (i32.xor (get_local $x11) (i32.rotl (i32.add (get_local $x10) (get_local $x9)) (i32.const 7))))
        (set_local $x8 (i32.xor (get_local $x8) (i32.rotl (i32.add (get_local $x11) (get_local $x10)) (i32.const 9))))
        (set_local $x9 (i32.xor (get_local $x9) (i32.rotl (i32.add (get_local $x8) (get_local $x11)) (i32.const 13))))
        (set_local $x10 (i32.xor (get_local $x10) (i32.rotl (i32.add (get_local $x9) (get_local $x8)) (i32.const 18))))
        (set_local $x12 (i32.xor (get_local $x12) (i32.rotl (i32.add (get_local $x15) (get_local $x14)) (i32.const 7))))
        (set_local $x13 (i32.xor (get_local $x13) (i32.rotl (i32.add (get_local $x12) (get_local $x15)) (i32.const 9))))
        (set_local $x14 (i32.xor (get_local $x14) (i32.rotl (i32.add (get_local $x13) (get_local $x12)) (i32.const 13))))
        (set_local $x15 (i32.xor (get_local $x15) (i32.rotl (i32.add (get_local $x14) (get_local $x13)) (i32.const 18))))
        (set_local $i (i32.sub (get_local $i) (i32.const 2)))
        (br $start_loop)
      )
    )

    (i32.store (get_local $out_ptr) (get_local $x0))
    (i32.store (i32.add (get_local $out_ptr) (i32.const 4)) (get_local $x5))
    (i32.store (i32.add (get_local $out_ptr) (i32.const 8)) (get_local $x10))
    (i32.store (i32.add (get_local $out_ptr) (i32.const 12)) (get_local $x15))
    (i32.store (i32.add (get_local $out_ptr) (i32.const 16)) (get_local $x6))
    (i32.store (i32.add (get_local $out_ptr) (i32.const 20)) (get_local $x7))
    (i32.store (i32.add (get_local $out_ptr) (i32.const 24)) (get_local $x8))
    (i32.store (i32.add (get_local $out_ptr) (i32.const 28)) (get_local $x9))
  )
)
