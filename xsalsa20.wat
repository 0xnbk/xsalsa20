(module
  (memory (export "memory") 10 1000)

  (func (export "xsalsa20_xor")
    (param $ptr i32) (param $c_ptr i32) (param $m_ptr i32) (param $m_len i32) (param $nonce_ptr i32) (param $key_ptr i32)
    (i64.store (local.get $ptr)
      (call $xsalsa20_xor_ic
        (local.get $c_ptr)
        (local.get $m_ptr)
        (local.get $m_len)
        (local.get $nonce_ptr)
        (i64.load (local.get $ptr))
        (local.get $key_ptr)
      )
    )
  )

  (func $xsalsa20_xor_ic
    (param $c_ptr i32) (param $m_ptr i32) (param $m_len i32) (param $nonce_ptr i32) (param $ic i64) (param $key_ptr i32) (result i64)

    ;; subkey --> 112-144
    (call $core_hsalsa20 (i32.const 112) (local.get $nonce_ptr) (local.get $key_ptr))

    (call $salsa20_xor_ic
      (local.get $c_ptr)
      (local.get $m_ptr)
      (local.get $m_len)
      (i32.add (local.get $nonce_ptr) (i32.const 16))
      (local.get $ic)
      (i32.const 112)
    )

    ;; memzero
    (i64.store (i32.const 112) (i64.const 0))
    (i64.store (i32.const 120) (i64.const 0))
    (i64.store (i32.const 128) (i64.const 0))
    (i64.store (i32.const 136) (i64.const 0))
  )

  (func $salsa20_xor_ic
    (param $c_ptr i32) (param $m_ptr i32) (param $m_len i32) (param $nonce_ptr i32) (param $ic i64) (param $key_ptr i32) (result i64)
    (local $i i32)

    ;; in -> 0-16
    ;; block -> 16-80
    ;; kcopy -> 80-112

    (if (i32.eq (local.get $m_len) (i32.const 0))
      (then
        (return (i64.const 0))
      )
    )

    ;; kcopy[i] = k[i];
    (i64.store (i32.const 80) (i64.load (local.get $key_ptr)))
    (i64.store (i32.const 88) (i64.load (i32.add (local.get $key_ptr) (i32.const 8))))
    (i64.store (i32.const 96) (i64.load (i32.add (local.get $key_ptr) (i32.const 16))))
    (i64.store (i32.const 104) (i64.load (i32.add (local.get $key_ptr) (i32.const 24))))

    ;; in[i] = n[i];
    (i64.store (i32.const 0) (i64.load (local.get $nonce_ptr)))

    ;; in[i] = (unsigned char) (ic & 0xff); (LE)
    (i64.store (i32.const 8) (local.get $ic))

    (block $end_loop
      (loop $start_loop
        (br_if $end_loop (i32.lt_u (local.get $m_len) (i32.const 64)))

        (call $core_salsa20 (i32.const 16) (i32.const 0) (i32.const 80))

        (i64.store (local.get $c_ptr)
          (i64.xor (i64.load (local.get $m_ptr)) (i64.load (i32.const 16)))
        )
        (i64.store (i32.add (local.get $c_ptr) (i32.const 8))
          (i64.xor (i64.load (i32.add (local.get $m_ptr) (i32.const 8))) (i64.load (i32.const 24)))
        )
        (i64.store (i32.add (local.get $c_ptr) (i32.const 16))
          (i64.xor (i64.load (i32.add (local.get $m_ptr) (i32.const 16))) (i64.load (i32.const 32)))
        )
        (i64.store (i32.add (local.get $c_ptr) (i32.const 24))
          (i64.xor (i64.load (i32.add (local.get $m_ptr) (i32.const 24))) (i64.load (i32.const 40)))
        )
        (i64.store (i32.add (local.get $c_ptr) (i32.const 32))
          (i64.xor (i64.load (i32.add (local.get $m_ptr) (i32.const 32))) (i64.load (i32.const 48)))
        )
        (i64.store (i32.add (local.get $c_ptr) (i32.const 40))
          (i64.xor (i64.load (i32.add (local.get $m_ptr) (i32.const 40))) (i64.load (i32.const 56)))
        )
        (i64.store (i32.add (local.get $c_ptr) (i32.const 48))
          (i64.xor (i64.load (i32.add (local.get $m_ptr) (i32.const 48))) (i64.load (i32.const 64)))
        )
        (i64.store (i32.add (local.get $c_ptr) (i32.const 56))
          (i64.xor (i64.load (i32.add (local.get $m_ptr) (i32.const 56))) (i64.load (i32.const 72)))
        )

        (i64.store (i32.const 8)
          (i64.add (i64.load (i32.const 8)) (i64.const 1))
        )

        (local.set $c_ptr (i32.add (local.get $c_ptr) (i32.const 64)))
        (local.set $m_ptr (i32.add (local.get $m_ptr) (i32.const 64)))
        (local.set $m_len (i32.sub (local.get $m_len) (i32.const 64)))
        (br $start_loop)
      )
    )

    (local.set $ic (i64.load (i32.const 8)))

    (if (i32.gt_u (local.get $m_len) (i32.const 0))
      (then
        (call $core_salsa20 (i32.const 16) (i32.const 0) (i32.const 80))

        (block $0
          (block $1
            (block $2
              (block $3
                (block $4
                  (block $5
                    (block $6
                      (block $7
                        (br_table $0 $1 $2 $3 $4 $5 $6 $7 (i32.div_u (local.get $m_len) (i32.const 8)))
                      )
                      (i64.store (i32.add (local.get $c_ptr) (i32.const 56))
                        (i64.xor (i64.load (i32.add (local.get $m_ptr) (i32.const 56))) (i64.load (i32.const 72)))
                      )
                    )
                    (i64.store (i32.add (local.get $c_ptr) (i32.const 48))
                      (i64.xor (i64.load (i32.add (local.get $m_ptr) (i32.const 48))) (i64.load (i32.const 64)))
                    )
                  )
                  (i64.store (i32.add (local.get $c_ptr) (i32.const 40))
                    (i64.xor (i64.load (i32.add (local.get $m_ptr) (i32.const 40))) (i64.load (i32.const 56)))
                  )
                )
                (i64.store (i32.add (local.get $c_ptr) (i32.const 32))
                  (i64.xor (i64.load (i32.add (local.get $m_ptr) (i32.const 32))) (i64.load (i32.const 48)))
                )
              )
              (i64.store (i32.add (local.get $c_ptr) (i32.const 24))
                (i64.xor (i64.load (i32.add (local.get $m_ptr) (i32.const 24))) (i64.load (i32.const 40)))
              )
            )
            (i64.store (i32.add (local.get $c_ptr) (i32.const 16))
              (i64.xor (i64.load (i32.add (local.get $m_ptr) (i32.const 16))) (i64.load (i32.const 32)))
            )
          )
          (i64.store (i32.add (local.get $c_ptr) (i32.const 8))
            (i64.xor (i64.load (i32.add (local.get $m_ptr) (i32.const 8))) (i64.load (i32.const 24)))
          )
        )
        (i64.store (local.get $c_ptr)
          (i64.xor (i64.load (local.get $m_ptr)) (i64.load (i32.const 16)))
        )
      )
    )

    ;; memzero
    (i64.store (i32.const 16) (i64.const 0))
    (i64.store (i32.const 24) (i64.const 0))
    (i64.store (i32.const 32) (i64.const 0))
    (i64.store (i32.const 40) (i64.const 0))
    (i64.store (i32.const 48) (i64.const 0))
    (i64.store (i32.const 56) (i64.const 0))
    (i64.store (i32.const 64) (i64.const 0))
    (i64.store (i32.const 72) (i64.const 0))
    (i64.store (i32.const 80) (i64.const 0))
    (i64.store (i32.const 88) (i64.const 0))
    (i64.store (i32.const 96) (i64.const 0))
    (i64.store (i32.const 104) (i64.const 0))

    (return (local.get $ic))
  )

  (func $core_hsalsa20 (param $out_ptr i32) (param $in_ptr i32) (param $key_ptr i32)
    (local $x0 i32)
    (local $x1 i32)
    (local $x2 i32)
    (local $x3 i32)
    (local $x4 i32)
    (local $x5 i32)
    (local $x6 i32)
    (local $x7 i32)
    (local $x8 i32)
    (local $x9 i32)
    (local $x10 i32)
    (local $x11 i32)
    (local $x12 i32)
    (local $x13 i32)
    (local $x14 i32)
    (local $x15 i32)
    (local $i i32)

    (local.set $x0 (i32.const 0x61707865))
    (local.set $x5 (i32.const 0x3320646e))
    (local.set $x10 (i32.const 0x79622d32))
    (local.set $x15 (i32.const 0x6b206574))

    (local.set $x1 (i32.load (local.get $key_ptr)))
    (local.set $x2 (i32.load (i32.add (local.get $key_ptr) (i32.const 4))))
    (local.set $x3 (i32.load (i32.add (local.get $key_ptr) (i32.const 8))))
    (local.set $x4 (i32.load (i32.add (local.get $key_ptr) (i32.const 12))))
    (local.set $x11 (i32.load (i32.add (local.get $key_ptr) (i32.const 16))))
    (local.set $x12 (i32.load (i32.add (local.get $key_ptr) (i32.const 20))))
    (local.set $x13 (i32.load (i32.add (local.get $key_ptr) (i32.const 24))))
    (local.set $x14 (i32.load (i32.add (local.get $key_ptr) (i32.const 28))))
    (local.set $x6 (i32.load (local.get $in_ptr)))
    (local.set $x7 (i32.load (i32.add (local.get $in_ptr) (i32.const 4))))
    (local.set $x8 (i32.load (i32.add (local.get $in_ptr) (i32.const 8))))
    (local.set $x9 (i32.load (i32.add (local.get $in_ptr) (i32.const 12))))

    ;; rounds
    (local.set $i (i32.const 20))
    (block $end_loop
      (loop $start_loop
        (br_if $end_loop (i32.eq (local.get $i) (i32.const 0)))
        (local.set $x4 (i32.xor (local.get $x4) (i32.rotl (i32.add (local.get $x0) (local.get $x12)) (i32.const 7))))
        (local.set $x8 (i32.xor (local.get $x8) (i32.rotl (i32.add (local.get $x4) (local.get $x0)) (i32.const 9))))
        (local.set $x12 (i32.xor (local.get $x12) (i32.rotl (i32.add (local.get $x8) (local.get $x4)) (i32.const 13))))
        (local.set $x0 (i32.xor (local.get $x0) (i32.rotl (i32.add (local.get $x12) (local.get $x8)) (i32.const 18))))
        (local.set $x9 (i32.xor (local.get $x9) (i32.rotl (i32.add (local.get $x5) (local.get $x1)) (i32.const 7))))
        (local.set $x13 (i32.xor (local.get $x13) (i32.rotl (i32.add (local.get $x9) (local.get $x5)) (i32.const 9))))
        (local.set $x1 (i32.xor (local.get $x1) (i32.rotl (i32.add (local.get $x13) (local.get $x9)) (i32.const 13))))
        (local.set $x5 (i32.xor (local.get $x5) (i32.rotl (i32.add (local.get $x1) (local.get $x13)) (i32.const 18))))
        (local.set $x14 (i32.xor (local.get $x14) (i32.rotl (i32.add (local.get $x10) (local.get $x6)) (i32.const 7))))
        (local.set $x2 (i32.xor (local.get $x2) (i32.rotl (i32.add (local.get $x14) (local.get $x10)) (i32.const 9))))
        (local.set $x6 (i32.xor (local.get $x6) (i32.rotl (i32.add (local.get $x2) (local.get $x14)) (i32.const 13))))
        (local.set $x10 (i32.xor (local.get $x10) (i32.rotl (i32.add (local.get $x6) (local.get $x2)) (i32.const 18))))
        (local.set $x3 (i32.xor (local.get $x3) (i32.rotl (i32.add (local.get $x15) (local.get $x11)) (i32.const 7))))
        (local.set $x7 (i32.xor (local.get $x7) (i32.rotl (i32.add (local.get $x3) (local.get $x15)) (i32.const 9))))
        (local.set $x11 (i32.xor (local.get $x11) (i32.rotl (i32.add (local.get $x7) (local.get $x3)) (i32.const 13))))
        (local.set $x15 (i32.xor (local.get $x15) (i32.rotl (i32.add (local.get $x11) (local.get $x7)) (i32.const 18))))
        (local.set $x1 (i32.xor (local.get $x1) (i32.rotl (i32.add (local.get $x0) (local.get $x3)) (i32.const 7))))
        (local.set $x2 (i32.xor (local.get $x2) (i32.rotl (i32.add (local.get $x1) (local.get $x0)) (i32.const 9))))
        (local.set $x3 (i32.xor (local.get $x3) (i32.rotl (i32.add (local.get $x2) (local.get $x1)) (i32.const 13))))
        (local.set $x0 (i32.xor (local.get $x0) (i32.rotl (i32.add (local.get $x3) (local.get $x2)) (i32.const 18))))
        (local.set $x6 (i32.xor (local.get $x6) (i32.rotl (i32.add (local.get $x5) (local.get $x4)) (i32.const 7))))
        (local.set $x7 (i32.xor (local.get $x7) (i32.rotl (i32.add (local.get $x6) (local.get $x5)) (i32.const 9))))
        (local.set $x4 (i32.xor (local.get $x4) (i32.rotl (i32.add (local.get $x7) (local.get $x6)) (i32.const 13))))
        (local.set $x5 (i32.xor (local.get $x5) (i32.rotl (i32.add (local.get $x4) (local.get $x7)) (i32.const 18))))
        (local.set $x11 (i32.xor (local.get $x11) (i32.rotl (i32.add (local.get $x10) (local.get $x9)) (i32.const 7))))
        (local.set $x8 (i32.xor (local.get $x8) (i32.rotl (i32.add (local.get $x11) (local.get $x10)) (i32.const 9))))
        (local.set $x9 (i32.xor (local.get $x9) (i32.rotl (i32.add (local.get $x8) (local.get $x11)) (i32.const 13))))
        (local.set $x10 (i32.xor (local.get $x10) (i32.rotl (i32.add (local.get $x9) (local.get $x8)) (i32.const 18))))
        (local.set $x12 (i32.xor (local.get $x12) (i32.rotl (i32.add (local.get $x15) (local.get $x14)) (i32.const 7))))
        (local.set $x13 (i32.xor (local.get $x13) (i32.rotl (i32.add (local.get $x12) (local.get $x15)) (i32.const 9))))
        (local.set $x14 (i32.xor (local.get $x14) (i32.rotl (i32.add (local.get $x13) (local.get $x12)) (i32.const 13))))
        (local.set $x15 (i32.xor (local.get $x15) (i32.rotl (i32.add (local.get $x14) (local.get $x13)) (i32.const 18))))
        (local.set $i (i32.sub (local.get $i) (i32.const 2)))
        (br $start_loop)
      )
    )

    (i32.store (local.get $out_ptr) (local.get $x0))
    (i32.store (i32.add (local.get $out_ptr) (i32.const 4)) (local.get $x5))
    (i32.store (i32.add (local.get $out_ptr) (i32.const 8)) (local.get $x10))
    (i32.store (i32.add (local.get $out_ptr) (i32.const 12)) (local.get $x15))
    (i32.store (i32.add (local.get $out_ptr) (i32.const 16)) (local.get $x6))
    (i32.store (i32.add (local.get $out_ptr) (i32.const 20)) (local.get $x7))
    (i32.store (i32.add (local.get $out_ptr) (i32.const 24)) (local.get $x8))
    (i32.store (i32.add (local.get $out_ptr) (i32.const 28)) (local.get $x9))
  )

  (func (export "core_salsa20") (param $out_ptr i32) (param $in_ptr i32) (param $key_ptr i32)
    (call $core_salsa20 (local.get $out_ptr) (local.get $in_ptr) (local.get $key_ptr))
  )

  (func $core_salsa20 (param $out_ptr i32) (param $in_ptr i32) (param $key_ptr i32)
    ;; uint32_t x0, x1, x2, x3, x4, x5, x6, x7, x8,
    ;;          x9, x10, x11, x12, x13, x14,  x15;
    ;; int      i;

    (local $x0 i32)
    (local $x1 i32)
    (local $x2 i32)
    (local $x3 i32)
    (local $x4 i32)
    (local $x5 i32)
    (local $x6 i32)
    (local $x7 i32)
    (local $x8 i32)
    (local $x9 i32)
    (local $x10 i32)
    (local $x11 i32)
    (local $x12 i32)
    (local $x13 i32)
    (local $x14 i32)
    (local $x15 i32)
    (local $j0 i32)
    (local $j1 i32)
    (local $j2 i32)
    (local $j3 i32)
    (local $j4 i32)
    (local $j5 i32)
    (local $j6 i32)
    (local $j7 i32)
    (local $j8 i32)
    (local $j9 i32)
    (local $j10 i32)
    (local $j11 i32)
    (local $j12 i32)
    (local $j13 i32)
    (local $j14 i32)
    (local $j15 i32)
    (local $i i32)

    (local.set $x0 (i32.const 0x61707865))
    (local.set $x5 (i32.const 0x3320646e))
    (local.set $x10 (i32.const 0x79622d32))
    (local.set $x15 (i32.const 0x6b206574))

    (local.set $x1 (i32.load (local.get $key_ptr)))
    (local.set $x2 (i32.load (i32.add (local.get $key_ptr) (i32.const 4))))
    (local.set $x3 (i32.load (i32.add (local.get $key_ptr) (i32.const 8))))
    (local.set $x4 (i32.load (i32.add (local.get $key_ptr) (i32.const 12))))
    (local.set $x11 (i32.load (i32.add (local.get $key_ptr) (i32.const 16))))
    (local.set $x12 (i32.load (i32.add (local.get $key_ptr) (i32.const 20))))
    (local.set $x13 (i32.load (i32.add (local.get $key_ptr) (i32.const 24))))
    (local.set $x14 (i32.load (i32.add (local.get $key_ptr) (i32.const 28))))
    (local.set $x6 (i32.load (local.get $in_ptr)))
    (local.set $x7 (i32.load (i32.add (local.get $in_ptr) (i32.const 4))))
    (local.set $x8 (i32.load (i32.add (local.get $in_ptr) (i32.const 8))))
    (local.set $x9 (i32.load (i32.add (local.get $in_ptr) (i32.const 12))))

    (local.set $j0 (local.get $x0))
    (local.set $j1 (local.get $x1))
    (local.set $j2 (local.get $x2))
    (local.set $j3 (local.get $x3))
    (local.set $j4 (local.get $x4))
    (local.set $j5 (local.get $x5))
    (local.set $j6 (local.get $x6))
    (local.set $j7 (local.get $x7))
    (local.set $j8 (local.get $x8))
    (local.set $j9 (local.get $x9))
    (local.set $j10 (local.get $x10))
    (local.set $j11 (local.get $x11))
    (local.set $j12 (local.get $x12))
    (local.set $j13 (local.get $x13))
    (local.set $j14 (local.get $x14))
    (local.set $j15 (local.get $x15))

    ;; rounds
    (local.set $i (i32.const 20))
    (block $end_loop
      (loop $start_loop
        (br_if $end_loop (i32.eq (local.get $i) (i32.const 0)))
        (local.set $x4 (i32.xor (local.get $x4) (i32.rotl (i32.add (local.get $x0) (local.get $x12)) (i32.const 7))))
        (local.set $x8 (i32.xor (local.get $x8) (i32.rotl (i32.add (local.get $x4) (local.get $x0)) (i32.const 9))))
        (local.set $x12 (i32.xor (local.get $x12) (i32.rotl (i32.add (local.get $x8) (local.get $x4)) (i32.const 13))))
        (local.set $x0 (i32.xor (local.get $x0) (i32.rotl (i32.add (local.get $x12) (local.get $x8)) (i32.const 18))))
        (local.set $x9 (i32.xor (local.get $x9) (i32.rotl (i32.add (local.get $x5) (local.get $x1)) (i32.const 7))))
        (local.set $x13 (i32.xor (local.get $x13) (i32.rotl (i32.add (local.get $x9) (local.get $x5)) (i32.const 9))))
        (local.set $x1 (i32.xor (local.get $x1) (i32.rotl (i32.add (local.get $x13) (local.get $x9)) (i32.const 13))))
        (local.set $x5 (i32.xor (local.get $x5) (i32.rotl (i32.add (local.get $x1) (local.get $x13)) (i32.const 18))))
        (local.set $x14 (i32.xor (local.get $x14) (i32.rotl (i32.add (local.get $x10) (local.get $x6)) (i32.const 7))))
        (local.set $x2 (i32.xor (local.get $x2) (i32.rotl (i32.add (local.get $x14) (local.get $x10)) (i32.const 9))))
        (local.set $x6 (i32.xor (local.get $x6) (i32.rotl (i32.add (local.get $x2) (local.get $x14)) (i32.const 13))))
        (local.set $x10 (i32.xor (local.get $x10) (i32.rotl (i32.add (local.get $x6) (local.get $x2)) (i32.const 18))))
        (local.set $x3 (i32.xor (local.get $x3) (i32.rotl (i32.add (local.get $x15) (local.get $x11)) (i32.const 7))))
        (local.set $x7 (i32.xor (local.get $x7) (i32.rotl (i32.add (local.get $x3) (local.get $x15)) (i32.const 9))))
        (local.set $x11 (i32.xor (local.get $x11) (i32.rotl (i32.add (local.get $x7) (local.get $x3)) (i32.const 13))))
        (local.set $x15 (i32.xor (local.get $x15) (i32.rotl (i32.add (local.get $x11) (local.get $x7)) (i32.const 18))))
        (local.set $x1 (i32.xor (local.get $x1) (i32.rotl (i32.add (local.get $x0) (local.get $x3)) (i32.const 7))))
        (local.set $x2 (i32.xor (local.get $x2) (i32.rotl (i32.add (local.get $x1) (local.get $x0)) (i32.const 9))))
        (local.set $x3 (i32.xor (local.get $x3) (i32.rotl (i32.add (local.get $x2) (local.get $x1)) (i32.const 13))))
        (local.set $x0 (i32.xor (local.get $x0) (i32.rotl (i32.add (local.get $x3) (local.get $x2)) (i32.const 18))))
        (local.set $x6 (i32.xor (local.get $x6) (i32.rotl (i32.add (local.get $x5) (local.get $x4)) (i32.const 7))))
        (local.set $x7 (i32.xor (local.get $x7) (i32.rotl (i32.add (local.get $x6) (local.get $x5)) (i32.const 9))))
        (local.set $x4 (i32.xor (local.get $x4) (i32.rotl (i32.add (local.get $x7) (local.get $x6)) (i32.const 13))))
        (local.set $x5 (i32.xor (local.get $x5) (i32.rotl (i32.add (local.get $x4) (local.get $x7)) (i32.const 18))))
        (local.set $x11 (i32.xor (local.get $x11) (i32.rotl (i32.add (local.get $x10) (local.get $x9)) (i32.const 7))))
        (local.set $x8 (i32.xor (local.get $x8) (i32.rotl (i32.add (local.get $x11) (local.get $x10)) (i32.const 9))))
        (local.set $x9 (i32.xor (local.get $x9) (i32.rotl (i32.add (local.get $x8) (local.get $x11)) (i32.const 13))))
        (local.set $x10 (i32.xor (local.get $x10) (i32.rotl (i32.add (local.get $x9) (local.get $x8)) (i32.const 18))))
        (local.set $x12 (i32.xor (local.get $x12) (i32.rotl (i32.add (local.get $x15) (local.get $x14)) (i32.const 7))))
        (local.set $x13 (i32.xor (local.get $x13) (i32.rotl (i32.add (local.get $x12) (local.get $x15)) (i32.const 9))))
        (local.set $x14 (i32.xor (local.get $x14) (i32.rotl (i32.add (local.get $x13) (local.get $x12)) (i32.const 13))))
        (local.set $x15 (i32.xor (local.get $x15) (i32.rotl (i32.add (local.get $x14) (local.get $x13)) (i32.const 18))))
        (local.set $i (i32.sub (local.get $i) (i32.const 2)))
        (br $start_loop)
      )
    )

    (i32.store (local.get $out_ptr) (i32.add (local.get $x0) (local.get $j0)))
    (i32.store (i32.add (local.get $out_ptr) (i32.const 4)) (i32.add (local.get $x1) (local.get $j1)))
    (i32.store (i32.add (local.get $out_ptr) (i32.const 8)) (i32.add (local.get $x2) (local.get $j2)))
    (i32.store (i32.add (local.get $out_ptr) (i32.const 12)) (i32.add (local.get $x3) (local.get $j3)))
    (i32.store (i32.add (local.get $out_ptr) (i32.const 16)) (i32.add (local.get $x4) (local.get $j4)))
    (i32.store (i32.add (local.get $out_ptr) (i32.const 20)) (i32.add (local.get $x5) (local.get $j5)))
    (i32.store (i32.add (local.get $out_ptr) (i32.const 24)) (i32.add (local.get $x6) (local.get $j6)))
    (i32.store (i32.add (local.get $out_ptr) (i32.const 28)) (i32.add (local.get $x7) (local.get $j7)))
    (i32.store (i32.add (local.get $out_ptr) (i32.const 32)) (i32.add (local.get $x8) (local.get $j8)))
    (i32.store (i32.add (local.get $out_ptr) (i32.const 36)) (i32.add (local.get $x9) (local.get $j9)))
    (i32.store (i32.add (local.get $out_ptr) (i32.const 40)) (i32.add (local.get $x10) (local.get $j10)))
    (i32.store (i32.add (local.get $out_ptr) (i32.const 44)) (i32.add (local.get $x11) (local.get $j11)))
    (i32.store (i32.add (local.get $out_ptr) (i32.const 48)) (i32.add (local.get $x12) (local.get $j12)))
    (i32.store (i32.add (local.get $out_ptr) (i32.const 52)) (i32.add (local.get $x13) (local.get $j13)))
    (i32.store (i32.add (local.get $out_ptr) (i32.const 56)) (i32.add (local.get $x14) (local.get $j14)))
    (i32.store (i32.add (local.get $out_ptr) (i32.const 60)) (i32.add (local.get $x15) (local.get $j15)))
  )
)
